' Gambas module file

''' Each of the User1-3 should be executed as seperate application
''' They give an example of sharing data between application.
''' Notifications are used to signal the apps as data changes
Public MyShare As ShmMem
Public Completed As Boolean = False
Public ValidVariables As String[] = ["cat", "dog", "rabbit", "done"]

Public Sub main()

    Dim MyShare As ShmMem
    Dim Buffer As String
    Dim elements As String[]
    Dim element As String[]

    ShmMem.ForceDelete("MySharedMem")                                          ' cleanup any old testing
    MyShare = New ShmMem(2000000, "MySharedMem", 8000)
    MyShare!Cat = "Felix"
    MyShare!Dog = "Rover"
    MyShare!Rabbit = 1000
    MyShare!Done = False

    MyShare.Notify("Cat", Me)
    MyShare.Notify("Dog", Me)
    MyShare.Notify("rabbit", Me)
    MyShare.Notify("Done", Me, "It_Is_Done")

    Print " User 1 monitoring all variables"
    While Not completed
        Print "Input Variable Name{cat,dog,rabbit,done} followed by an = and the new value\nExample: Cat=this"
        Line Input Buffer
        element = Split(buffer, "=", "\"", True, False)
        If element.count <> 2 Or If Not ValidVariables.exist(element[0]) Then
            Print "Invalid input :"; buffer
            Continue
        Endif
        MyShare[element[0]] = element[1]
        PrintStuff(MyShare)
        Wait 0.001
    Wend

    Print "User 1 Completed and exits"

End

Public Sub PrintStuff(MyShare As ShmMem)

    Dim Value As Variant = ""

    For Each Value In MyShare
        Print MyShare.key; "="; Quote(Value)
    Next

End

Public Sub cat_changed(varname As String, value As Variant, TimedOut As Boolean)

    Print "Sent  ";; value;; " For Cat"

End

Public Sub dog_changed(varname As String, value As Variant, TimedOut As Boolean)

    Print "Sent ";; value;; " For Dog"

End

Public Sub Rabbit_changed(varname As String, value As Variant, TimedOut As Boolean)

    Print "Sent";; value;; "for rabbit"

End

Public Sub It_Is_Done(varname As String, value As Variant, TimedOut As Boolean)

    If value == "True" Then
        Completed = True
        Print "Done Is committed"
    Else
        Print "done set to "; value
    Endif

End
