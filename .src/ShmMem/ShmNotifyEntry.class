' Gambas class file

' Author Westwood JustLostintime
'Released Under:
'  GNU GENERAL Public LICENSE
'  Version 3, 29 June 2007
'  See project license file.

Export
'Fast Unsafe

Public Struct AllocHeader                   '' Standard header
    Length As Long                          '' Length including header
    Address As Pointer                      '' The virtual address of this block, speed pointer conversion
    Type As Integer                         '' Type of memory
    {Next} As Pointer                       '' null if no more in list
End Struct

Public Struct dPointer
    Length As Long                          '' Length including header
    Address As Pointer                      '' The virtual address of this block, speed pointer conversion
    Type As Integer                         '' Type of memory
    Value As Pointer                        '' The actual value for usage
End Struct

Public Struct Symbol
    Name[128] As Byte                         '' Max length of any variable name
    Used As Boolean                             '' This slot is in use
    NotifyBlock As Pointer                  '' pointer to chain of pids waiting to be notifies when This symbol changes
    Address As Long                             '' virtual address of the value, 0 means no value yet
    Hash As Long                                    '' When we start using a hash for this table
    NameLength As Integer                '' NameLength
    {Next} As Long                                '' in case of a hash conflict index to the next in this chain -1 of not used
    Previous As Long                            '' in case of hash conflict index to the parent of this value -1 if not used
    Type As Integer                                '' data type of the data stored into the memory, -1 if gambas data type
End Struct

Public NotifyObject As Object                               '' The Object to raise the notify event with
Public NotifyFunc As String                                    '' Name of the function to call when being notifies
Public NotifyTimeout As Integer                          '' Number of  ticks when waiting for a notification
Public NotifyVar As Symbol                                   '' The variable symbol table entry  we are waiting for a change
Public VarName As String                                      '' text name of the variable we are waiting to change
Public VarMemory As ShmVarStorage              '' The memory that containg the variable values
Public SymTable As ShmSymbolTable               '' the symbol table associated with the entry we are waiting for
Public Tag As String                                                 '' a tag for uniqueue identification of an entry
Public StartTime As Float                                       '' time swe started waiting
Private VarClock As Timer                                    '' the timer associated with this variable
Private OneShot As Boolean = False                 '' Wait just once for a change, then delete the notify rerquest
' Private OneShot As Boolean

Event VarSpecial(VarDef As ShmNotifyEntry, Info As Variant[])                     '' sent when a new variable is created
Event VarChanged(VarDef As ShmNotifyEntry)                                                    '' sent when a variable value changes
Event VarTimedOut(VarDef As ShmNotifyEntry)                                                 '' Sent when a variable times out

Extern sched_yield() As Integer In "libpthread:0"

'' Dispatch Functions must look like
'' MyChange(SymSource as ShmMem, SymName as String, Value as Variant, TimedOut as boolean)
Public Sub _new(SymName As Symbol, CallObject As Object, CallFunc As String, NotifyMem As ShmNotify, Optional fTimeout As Long = 0, Once As Boolean = False, mTag As String = "")

    NotifyObject = CallObject
    NotifyFunc = CallFunc
    NotifyVar = SymName
    NotifyTimeOut = fTimeOut
    VarMemory = NotifyMem.VarMem
    SymTable = NotifyMem.SymMem
    oneShot = Once
    VarName = SymName.Name.ToString()
    If mTag = "" Then
        Tag = Str(CallObject) & CallFunc
    Else
        Tag = mTag
    Endif

    If fTimeOut > 0 Then
        VarClock = New Timer As "VarTimeout"
        VarClock.Delay = fTimeout
        VarClock.Enabled()
    Endif

End

Public Sub _free()

    SymTable = Null
    VarMemory = Null
    VarClock = Null
    NotifyVar = Null
    NotifyObject = Null

End

'' the timer management
Public Sub VarTimeOut_Timer()

    Dim Result As Boolean

    result = Raise VarTimedOut(NotifyVar)
    VarClock.Stop()

End

'' Called when a new variable is created, raises the event
Public Sub NewVal(Optional extendedInfo As Variant[] = Null)

    If extendedInfo.count > 1 Then
        Raise VarSpecial(Me, extendedInfo)
    Else
        Raise VarChanged(Me)
    Endif
    Sched_Yield()

End

'' Called when a signal arrives from another task, and an event is generated
Public Sub Dispatch(Failed As Boolean, Optional NameOverride As String = "")

    Dim onnew As Boolean = (NameOverRide <> "")
    Dim ReportedVar As String
    Dim value As Variant

    If onnew Then
        ReportedVar = NameOverRide     '' check for the special variable
    Else
        ReportedVar = varname
    Endif

    VarMemory.Lock()
    If onnew Then
        value = SymTable[ReportedVar]
    Else
        value = VarMemory[NotifyVar.Address]
    Endif
    VarMemory.Unlock()

    Dim ParmList As Variant[] = [ReportedVar, value, Failed]

    Object.Call(NotifyObject, NotifyFunc, ParmList)
    Sched_Yield()

End
